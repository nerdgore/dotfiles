#!/usr/bin/osascript
--
-- A small command line script to change screen resolutions on Mountain Lion.
--
-- Primarily I switch between two resolutions on my Retina MacBook Pro: Retina,
-- and the full resolution setting. This means for particular apps I use, I can
-- quickly jump between seeing more pixels and less.
--
-- There doesn't appear to be an easy way to do this without just using
-- AppleScript to automate clicking buttons, so that's what this does.
--
-- Most of this script is adapted from this helpful answer:
--
--   http://apple.stackexchange.com/a/91590
--
-- Make sure "Enable access for assistive devices" is checked in the
-- Accessibility section of System Properties.

local index1, index2
-- choices
-- 1 -- 1024 x 640 (Largest Text)
-- 2 -- 1280 x 800 (Larger Text)
-- 3 -- 1440 x 900 (Best for Retina) [Default on El Capitan]
-- 4 -- 1680 x 1050(More Space) [Default on Sierra]
-- 5 -- 1920 x 1200 (Most Space)

set index1 to 4
set index2 to 5

tell application "System Preferences"
	reveal anchor "displaysDisplayTab" of pane "com.apple.preference.displays"
end tell

local indexToUse

tell application "System Events" to tell process "System Preferences" to tell window "Built-in Retina Display"

	click radio button "Display" of tab group 1
	-- TODO: check for Default resolution rather than 2 indizes
	-- Click the "Scaled" radio button
	click radio button "Scaled" of radio group 1 of tab group 1

	-- group 1 contains the brightness slider
	tell radio group 1 of group 2 of tab group 1
		set isRetinaOptimized to get value of radio button index1
	end tell

	if isRetinaOptimized then
		set indexToUse to index2
	else
		set indexToUse to index1
	end if

	click radio button indexToUse of radio group 1 of group 2 of tab group 1

	-- If option/index 1 is selected a warning prompt is displayed, click the OK button to dismiss the prompt
	if indexToUse = 1 then
		click button "OK" of sheet 1
	end if
end tell

tell application "System Preferences"
	quit
end tell
